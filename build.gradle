buildscript {
    ext.kotlin_version = "1.3.31"
    ext.springBootVersion = "2.1.2.RELEASE"

    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"

        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
        classpath "org.jetbrains.kotlin:kotlin-allopen:${kotlin_version}"

        classpath "gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.13.0"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.0.0-RC14"
    }
}

apply plugin: "kotlin"
apply plugin: "kotlin-spring"
apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"
apply plugin: "com.palantir.docker"
apply plugin: "io.gitlab.arturbosch.detekt"

group   "org.rentalhouse"
version "1.0-SNAPSHOT"

repositories {
    mavenCentral()
    jcenter()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Greenwich.RELEASE"
    }
}

dependencies {

    compile             "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile             "org.springframework.boot:spring-boot-starter-data-mongodb"
    compile             "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.7"
    compile             "org.springframework.cloud:spring-cloud-starter-netflix-eureka-client"
    compile             "org.springframework.cloud:spring-cloud-stream"
    compile             "org.springframework.cloud:spring-cloud-starter-stream-kafka"
    compile             "org.springframework.cloud:spring-cloud-starter-sleuth"

    implementation      "org.springframework.boot:spring-boot-starter-web"

    testCompile         "org.assertj:assertj-core:3.12.2"
    testCompile         "org.springframework.boot:spring-boot-starter-test"
    testCompile         "au.com.dius:pact-jvm-provider-junit5_2.12:3.6.5"
    testCompile         ("au.com.dius:pact-jvm-consumer-junit5_2.12:3.6.5") {
        exclude group: "org.json"
    }
    testCompile         "org.springframework.cloud:spring-cloud-stream-test-support"

    testImplementation  "io.mockk:mockk:1.9"
    testImplementation  "org.junit.jupiter:junit-jupiter:5.4.1"
    testImplementation  "de.flapdoodle.embed:de.flapdoodle.embed.mongo:2.2.0"
}

test {
    useJUnitPlatform()
    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
}

task unpack(type: Copy) {
    dependsOn bootJar
    from(zipTree(tasks.bootJar.outputs.files.singleFile))
    into("build/dependency")
}

docker {
    name "${project.group}/${bootJar.baseName}"
    copySpec.from(tasks.unpack.outputs).into("dependency")
    buildArgs(['DEPENDENCY': "dependency"])
}

detekt {
    config = files("src/main/resources/default-detekt-config.yml")
    reports {
        html {
            enabled = true
            destination = file("build/deteKt.html")
        }
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

build.dependsOn test